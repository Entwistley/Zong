defaultTasks 'dist'

//definition of subprojects
subprojects {
	apply plugin: 'java'
	
	sourceCompatibility = 6
	
	repositories {
		mavenCentral()
	}
	
	//additional configurations for compile classpaths
	configurations {
		compileOnly
		testCompileOnly { extendsFrom compileOnly }
	}
	
	dependencies {
		compile files('../utils/build/dist/onejar/utils.jar')
		compileOnly group: "org.projectlombok", name: "lombok", version: "1.14.2"
		testCompile group: 'junit', name: 'junit', version: '4.+'
	}

	sourceSets {
		main {
			java { srcDir 'src' }
			compileClasspath += configurations.compileOnly
		}
		test {
			java { srcDir 'test' }
			compileClasspath += configurations.compileOnly + configurations.testCompileOnly
		}
	}

	jar {
		from 'license.txt'
		from 'readme.txt'
		manifest.attributes 'Created-By': 'Xenoage Software'
	}
}

//project dependencies
project(':converter') { dependencies { compile project(':desktop') } }
project(':desktop') {
	sourceCompatibility = 8
	dependencies {
		compile project(':renderer'), project(':midi-out'), project(':musicxml-in'), project(':voc')
		compile group: "org.controlsfx", name: "controlsfx", version: "8.0.6"
		compile group: "com.itextpdf", name: "itextpdf", version: "5.4.5"
		compile files('../lib/lib/vorbis-java-1.0.0-beta.jar')
	}
}
project(':layout') { dependencies { compile project(':core') } }
project(':midi-out') { dependencies { compile project(':core') } }
project(':musicxml') { dependencies { compile project(':core') } }
project(':musicxml-in') { dependencies { compile project(':musicxml') } }
project(':player') {
	sourceCompatibility = 8
	dependencies { compile project(':desktop') }
}
project(':renderer') { dependencies { compile project(':layout') } }
project(':test') { dependencies { compile project(':converter'), project(':webserver') } }
project(':voc') { dependencies { compile project(':core') } }
project(':webserver') {
	dependencies {
		compile project(':desktop')
		compile group: "com.google.code.gson", name: "gson", version: "2.2.4"
		compile group: "com.h2database", name: "h2", version: "1.3.174"
		compile group: "org.eclipse.jetty", name: "jetty-server", version: "9.1.0.v20131115"
		compile group: "org.eclipse.jetty", name: "jetty-servlet", version: "9.1.0.v20131115"
	}
}

//update lib
task updateLib {
	println 'Update libraries'
	ant.java(jar:"lib/sync.jar", fork:true, dir:"lib")
}

//create JavaDoc
task createJavaDoc {
	println 'Create JavaDoc'
	ant.importBuild 'javadoc.build.xml'
	dependsOn antZongJavadoc
}

//build dist of Zong!
task dist {
	//update libraries
	dependsOn updateLib
	//first, build utils
	dependsOn 'buildUtils'
	//then, build the subprojects
	dependsOn subprojects*.build
	//then, create javadocs (Ant does this better)
	dependsOn createJavaDoc
}

//clean Zong!
task clean {
	//also clean Xenoage Utils
	dependsOn 'cleanUtils'
}

//build dist of Xenoage Utils
task buildUtils(type: GradleBuild) {
	buildFile = 'utils/build.gradle'
}

//build dist of Xenoage Utils
task cleanUtils(type: GradleBuild) {
	buildFile = 'utils/build.gradle'
	tasks = ['clean']
}
