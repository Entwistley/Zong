//***************************
// Buildscript Setup
//***************************

buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'de.richsource.gradle.plugins:gwt-gradle-plugin:0.6'
	}
}

plugins {
	id 'io.franzbecker.gradle-lombok' version '1.8'
}

apply plugin: 'gwt'

import io.franzbecker.gradle.lombok.task.DelombokTask



//***************************
// Delombok Zong! sources for GWT
//***************************

//all source folders of the projects the webapp project depends on
def delombok_src = [
		"../core/src",
		"../layout/src",
		"../renderer/src",
		"../utils/utils-base/src/",
		"../utils/utils-gwt/src/",
		"../utils/utils-kernel/src/"
]
//destination folder for delomboked sources
def delombok_target = "src-zong"

//delombok
task prepareSrcZong(type: DelombokTask) {
	ext.outputDir = file(delombok_target)
	outputs.dir(outputDir)
	delombok_src.each {
		inputs.dir(it)
		args(it, "-d", outputDir)
	}
}

//clean destination folder before delomboking
prepareSrcZong.doFirst {
	delete 'src-zong'
}

//remove PlatformUtilsBootstrap after delomboking, since
//this class is implemented differently in the project (no reflection in GWT possible)
prepareSrcZong.doLast {
	delete 'src-zong/com/xenoage/utils/PlatformUtilsBootstrap.java'
}



//***************************
// GWT compiler
//***************************

//all sources are needed, and Java compilation is called before
//(no idea why, but GWT Gradle plugin seems to work this way)
compileJava.dependsOn prepareSrcZong

//GWT compiler config
gwt {
	gwtVersion = '2.8.0'
	modules 'com.xenoage.zong.webapp.webapp'
	compiler { style = 'OBF' } //alternative: PRETTY
	//more memory required, otherwise OutOfMemoryError
	minHeapSize = "512M";
	maxHeapSize = "1024M";
}

//where to find the sources
sourceSets {
	main {
		java {
			srcDir 'src'
			srcDir 'src-zong'
		}
		resources {
			srcDir 'src'
			srcDir 'src-zong'
		}
	}
}

//copy webapp (JS results) and WEB-INF folder from Gradle build folder into www folder
compileGwt.doLast {
	delete 'www/webapp', 'www/WEB-INF'
	copy { from 'build/gwt/out/' into "www/" }
}